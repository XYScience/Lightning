plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.redteamobile.lightning"
        minSdkVersion 28
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        // Dev:"0"; QA:"1"; Staging:"2"; Prod:"3";
        buildConfigField 'int', 'serverType', '1'
        buildConfigField "boolean", "log", "false"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        platform {
            keyAlias 'platform'
            keyPassword 'android'
            storeFile file('../platform.keystore')
            storePassword 'android'
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "log", "true"
            minifyEnabled false
            signingConfig signingConfigs.platform
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.platform
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                def appName = "Lightning"

                def versionName = defaultConfig.versionName
                def versionCode = defaultConfig.versionCode

                def serverType = defaultConfig.getBuildConfigFields().get("serverType").value
                def serverTypeName = ""
                switch (serverType) {
                    case "0":
                        serverTypeName = "Dev"
                        break
                    case "1":
                        serverTypeName = "QA"
                        break
                    case "2":
                        serverTypeName = "Staging"
                        break
                    case "3":
                        serverTypeName = "Prod"
                        break
                    default:
                        serverTypeName = "unset"
                        break
                }

                def isSigned = (variant.buildType.getSigningConfig() != null)
                println("is Signed = ${isSigned}")

                // SellCardAuto-QA-1.0.0-1-release-signed.apk
                outputFileName = "${appName}-${serverTypeName}-${versionName}-${versionCode}-${variant.buildType.name}-${isSigned ? 'signed' : 'unsigned'}.apk"

                variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/build/outputs/apk")
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.1'
    implementation 'androidx.navigation:navigation-ui:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    implementation "io.reactivex.rxjava2:rxjava:2.2.2"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha2'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.google.guava:guava:23.5-android'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'fr.bmartel:jspeedtest:1.32.1'
}